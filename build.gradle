plugins {
    id 'fabric-loom' version '1.13-SNAPSHOT'
}

archivesBaseName = archives_base_name
version = mod_version
group = maven_group

repositories {
    mavenCentral()
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    mavenLocal()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"

    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // mods
    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
    modRuntimeOnly "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}:fabric-stub"

    modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${voicechat_mod_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modApi "com.terraformersmc:modmenu:${mod_menu_version}"

    // tts providers (turns out there is a freetts maven)
    implementation "net.sf.sociaal:freetts:${freetts_version}"
    include "net.sf.sociaal:freetts:${freetts_version}"
}

processResources {
    inputs.property "version", version

    filesMatching("fabric.mod.json") {
        expand "version": mod_version,
                "java_version": 21,
                "minecraft_version": minecraft_version,
                "loader_version": loader_version,
                "fabric_version": fabric_version,
                "voicechat_api_version": voicechat_api_version,
                "cloth_config_version": cloth_config_version,
                "mod_menu_version": mod_menu_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }

    from(zipTree(files("libs/cmu_us_kal.jar").singleFile))
    from(zipTree(files("libs/cmulex.jar").singleFile))
    from(zipTree(files("libs/en_us.jar").singleFile))
    from(zipTree(files("libs/freetts.jar").singleFile))
    from(zipTree(files("libs/jsapi.jar").singleFile))
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}